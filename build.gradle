import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.7.0'
}

group = 'com.m4kvn.spigot'
version = '0.1.2'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

dependencies {
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])
    compileOnly 'org.spigotmc:spigot-api:1.19-R0.1-SNAPSHOT'
    embed "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

jar {
    exclude "META-INF/versions/9/module-info.class"
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
}

task copyJarToServer(type: Copy) {
    from jar
    into "server/plugins"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

build.dependsOn copyJarToServer